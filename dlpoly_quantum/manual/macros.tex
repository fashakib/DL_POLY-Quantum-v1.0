
\subsection{Useful Macros}
\label{macros}
\subsubsection{Macros}
Macros are simple executable files containing standard linux commands.
A number of the are supplied with DL\_POLY and are found in the
{\em execute} sub-directory\index{sub-directory}. The available macros are as follows.
{\sl
\begin{itemize}
\item cleanup
\item copy
\item gopoly
\item gui
\item select
\item store
\item supa
\end{itemize}
}

The function of each of these is described below. It is worth noting
that most of these functions can be performed by the \D{} java GUI
\cite{smith-gui}. (It may be necessary to set the {\em execute} access
to the macro using the linux command:

\noindent chmod -x {\em macro}

\noindent where {\em macro} is one of the above names.)

\subsubsection{{\sl cleanup}}

{\sl cleanup} removes several standard data files from the {\em
execute} sub-directory\index{sub-directory}. It contains the linux commands:
\begin{verbatim}
#!/bin/tcsh
#
# DL_POLY utility to clean up after a program run
#
if (-e  CFGMIN) rm CFGMIN
if (-e  OUTPUT) rm OUTPUT
if (-e  RDFDAT) rm RDFDAT
if (-e  REVCON) rm REVCON
if (-e  REVIVE) rm REVIVE
if (-e  REVOLD) rm REVOLD
if (-e  STATIS) rm STATIS
if (-e  ZDNDAT) rm ZDNDAT
\end{verbatim}
\noindent and removes the files (if present) CFGMIN, OUTPUT, REVCON, 
REVOLD, STATIS, REVIVE, RDFDAT and ZDNDAT. (Useful data should
be stored elsewhere beforehand!)

\subsubsection{{\sl copy}}

{\sl copy} invokes the linux commands:

\begin{verbatim}
#!/bin/tcsh
#
# utility to set up data for DL_POLY continuation run
#
mv CONFIG CONFIG.OLD
mv REVCON CONFIG
mv REVIVE REVOLD
\end{verbatim}

\noindent which collectively prepare the DL\_POLY files in the {\em execute}
sub-directory\index{sub-directory} for the continuation of a simulation. It is
always a good idea to store these files elsewhere in addition to using this
macro.

\subsubsection{{\sl gopoly}}

{\sl gopoly} is a simple script to submit a DL\_POLY job to a standard linux parallel machine.

\begin{verbatim}
mpirun -np $1 DLPOLY.X
\end{verbatim}

\noindent Normally the job is submitted by the linux command:\\~\\
{\sl gopoly  8}\\~\\
\noindent where (in this case) {\sl 8} specifies the use of 8 processors.

If the {\em serial} version of \DD{} is being used it is of course
acceptable to simply type:

\begin{verbatim}
DLPOLY.X &
\end{verbatim}

\subsubsection{\sl gui}

{\sl gui} is a macro that starts up the \D{} Java GUI. It invokes the
following linux commands:

\begin{verbatim}
java -jar ../java/GUI.jar
\end{verbatim}

In other words the macro invokes the Java Virtual Machine which
executes the instructions in the Java archive file GUI.jar, which is
stored in the {\em java} subdirectory of \D{}. (Note: Java 1.3.0 or a
higher version is required to run the GUI.)

\subsubsection{{\sl select}}

{\sl select} is a macro enabling easy selection of one of the test
cases. It invokes the linux commands:

\begin{verbatim}
#!/bin/tcsh
#
# DL_POLY utility to gather test data files for program run
#
cp ../data/TEST$1/$2/CONTROL CONTROL
cp ../data/TEST$1/$2/FIELD   FIELD
cp ../data/TEST$1/$2/CONFIG  CONFIG
if (-e  ../data/TEST$1/$2/TABLE)then
  cp ../data/TEST$1/$2/TABLE   TABLE
else if (-e  ../data/TEST$1/$2/TABEAM)then
  cp ../data/TEST$1/$2/TABEAM  TABEAM
endif
\end{verbatim}
\noindent {\sl select} requires two arguments to be specified:\\~\\
{\sl select n a}\\~\\
\noindent where {\sl n} is the (integer) test case number, which
ranges from 1 to 20 and {\sl a} is the character string LF, VV, RB or
CB according to which algorithm leapfrog (LF), velocity Verlet (VV),
(RB) rigid body minimisation or (CB) constraint bond minimisation is
required.

This macro sets up the required input files in the {\em execute}
sub-directory\index{sub-directory} to run the {\sl n}-th test case.

\subsubsection{{\sl store}}

The {\sl store} macro provides a convenient way of moving data back
from the {\em execute} sub-directory\index{sub-directory} to the {\em
data} sub-directory\index{sub-directory}. It invokes the linux
commands:

\begin{verbatim}
#!/bin/tcsh
#
# DL_POLY utility to archive I/O files to the data directory
#
if !(-e ../data/TEST$1) then
  mkdir ../data/TEST$1
endif
if !(-e ../data/TEST$1/$2) then
  mkdir ../data/TEST$1/$2
endif
mv CONTROL ../data/TEST$1/$2/CONTROL
mv FIELD ../data/TEST$1/$2/FIELD
mv CONFIG ../data/TEST$1/$2/CONFIG
mv OUTPUT ../data/TEST$1/$2/OUTPUT
mv REVIVE ../data/TEST$1/$2/REVIVE
mv REVCON ../data/TEST$1/$2/REVCON
if (-e TABLE) then
  mv TABLE ../data/TEST$1/$2/TABLE
endif
if (-e TABEAM) then
  mv TABEAM ../data/TEST$1/$2/TABEAM
endif
if (-e STATIS) then
  mv STATIS ../data/TEST$1/$2/STATIS
endif
if (-e RDFDAT) then
  mv RDFDAT ../data/TEST$1/$2/RDFDAT
endif
if (-e ZDNDAT) then
  mv ZDNDAT ../data/TEST$1/$2/ZDNDAT
endif
if (-e CFGMIN) then
  mv CFGMIN ../data/TEST$1/$2/CFGMIN
endif
\end{verbatim}

\noindent which first creates a new DL\_POLY {\em data/TEST..}
sub-directory\index{sub-directory} if necessary and then moves the
standard DL\_POLY output data files into it.

{\sl store} requires two arguments:\\~\\
{\sl store n a}\\~\\
\noindent where {\sl n} is a unique string or number to label the
output data in the {\em data/TESTn} sub-directory and {\sl a} is the
character string LF, VV, RB or CB according to which algorithm
leapfrog (LF), velocity Verlet (VV), (RB) rigid body minimisation or
(CB) constraint bond minimisation has been performed.

\subsubsection{{\sl supa}}
The {\sl supa} macro provides a convenient way of running the DL\_POLY test
cases in batch mode. It is currently structured to submit batch jobs to the
Daresbury Xeon cluster, but can easily be adapted for other machines where
batch queuing is possible. The key statement in this context in the `qsub'
commmand which submits the {\sl gopoly} script described above. This statement
may be replaced by the equivalent batch queuing command for your machine. The
text of {\sl supa} is given below.
\begin{verbatim}
#!/bin/tcsh
#
# DL_POLY script to run multiple test cases
# note use of qsub in job submission - may
# need replacing
#

set n=$1
set m=$2
set TYPE="LF VV CB RB"

while ($n <= $m)
  if !(-e  TEST$n) mkdir TEST$n
  cd TEST$n
  echo TEST$n
  foreach typ ($TYPE)
    if (-e ../../data/TEST$n/$typ ) then
      if !(-e  $typ) mkdir $typ
      cd $typ
      cp ../../../data/TEST$n/$typ/CONTROL .
      cp ../../../data/TEST$n/$typ/CONFIG .
      cp ../../../data/TEST$n/$typ/FIELD .
      if (-e  ../../../data/TEST$n/$typ/TABLE) \
      cp ../../../data/TEST$n/$typ/TABLE .
      if(-e  ../../../data/TEST$n/$typ/TABEAM) \
      cp ../../../data/TEST$n/$typ/TABEAM .
      qsub ../../gopoly
      cd ../
    endif
  end
  cd ../
  set n=`expr $n + 1`
end
\end{verbatim}

\noindent This macro creates working {\em TEST} directories in 
the {\em execute} sub-directory\index{sub-directory}; one for each
test case invoked. Appropriate sub-directories of these are created for
leapfrog (LF), velocity Verlet(VV), rigid body minimisation (RB) and
constraint bond minimisation (CB). Note that {\sl supa} must be run
from the {\em execute} sub-directory.

{\sl supa} requires two arguments:\\~\\
{\sl supa n m}\\~\\
\noindent where {\sl n} and {\sl m} are integers defining the first
and last test case to be run.
\clearpage

